

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.ac.bg.etf.pp1.CompilerError.CompilerErrorType;
import java.util.*;

parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
    public List<CompilerError> synErrors = new ArrayList<>();
   
   	public List<CompilerError> getSynErrors(){
    	return synErrors;
    }
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
    
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)  
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, ENUM, CONST, PRINT, READ, RETURN, VOID, NEW;
terminal DO, WHILE;
terminal LBRACE, RBRACE, SEMI, LPAREN, RPAREN, LBRACKET, RBRACKET, QMARK, COLON;

terminal PLUS, MINUS, MUL, DIV, MOD;
terminal EQ, NEQ, GT, GTEQ, LT, LTEQ;
terminal AND, OR, EQUAL, INC, DEC;
terminal COMMA, POINT;

terminal Integer NUMBER;
terminal String BOOL;
terminal Character CHAR;
terminal String IDENT;
terminal IF, ELSE;

terminal SWITCH, CASE, BREAK, CONTINUE, CLASS, EXTENDS;

nonterminal MethodDeclList;
nonterminal FormalParamList, FormalParamDecl, FormPars, ActualPars, ActualParamList, RetType; 
nonterminal StatementList, Statement, Addop, Muloper, Assignop, Relop, Minus;

nonterminal DesignatorStatement, IfInsideStmt, ElseInsideStmt, IfPrepare, IfElseInsideStmt;
nonterminal DeclList, VarDecl, ConstDeclList, ConstDecl, ConstName;

nonterminal MethodDecl, Else, DesExpr, VarDeclComma, CaseList, DoStart, WhileEnd, ArrSize; 


nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, VarDeclList, Factor, CondFact, CondTerm, Condition, FuncCall;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, ProgName, Designator, MethodTypeName, DesArrayName, Expr, Term, TermSum;

nonterminal BasicTypes, CondInParens;

precedence left ELSE;

Program ::= (Program) PROG ProgName:p DeclList LBRACE MethodDeclList RBRACE ;

ProgName ::= (ProgName) IDENT:progName;


DeclList ::= (ASTVarDeclList) DeclList Type VarDeclList:varDeclList
	| 
	 (ASTConstDeclList) DeclList CONST Type ConstDeclList SEMI
	|
	 (NoSpecDecl) /* epsilon */
	;

VarDeclList ::= (MoreVarDecls) VarDeclComma VarDeclList
	|
	  (SingleVarDecl) VarDecl:varDecl SEMI
	|
	  (ErrorVarSemi) error:e SEMI 
	  {: parser.report_error("Sintaksna greska. Izvrsen oporavak do ; " + eleft, null); 
	  parser.synErrors.add(new CompilerError(eleft, "Greska u definiciji globalne promenljive. oporavak do ';'", CompilerErrorType.SYNTAX_ERROR)); :}
	;
		
VarDeclComma ::= (ASTVarDeclComma) VarDecl:varDecl COMMA
		|
		  (ErrorVarComma) error:e COMMA
		  {: parser.report_error("Sintaksna greska. Izvrsen oporavak do , u liniji " + eleft, null);
		  parser.synErrors.add(new CompilerError(eleft, "Greska u definiciji globalne promenljive. oporavak do ','", CompilerErrorType.SYNTAX_ERROR));  :}
		;		
		
		
		
VarDecl ::= (ASTVarDecl) IDENT:var
	|
	(ArrayDecl) IDENT:var LBRACKET RBRACKET
	;



ConstDeclList ::= (MoreConstDecls) ConstDeclList COMMA ConstDecl 
		| 
		  (SingleConstDecl) ConstDecl
		;
		


ConstDecl ::= (ConstDecl) ConstName EQUAL BasicTypes;


ConstName ::= (ConstName)  IDENT:var;

MethodDeclList ::= (ASTMethodDecList) MethodDeclList MethodDecl
				|
					(NoMethodDecl)	/* epsilon */
				;
			
MethodDecl ::= (MethodDecl)	MethodTypeName LPAREN FormPars RPAREN DeclList LBRACE StatementList RBRACE ;


MethodTypeName ::= (MethodTypeName) RetType:retType IDENT:methName;


RetType ::= (ActualType) Type 
			|
			(NoType) VOID
			;
			
			
FormPars ::= (FormParams) FormalParamList
		|
			(NoFormParams)	/* epsilon */
		;
		
		
FormalParamList ::= (FormalParamDecls) FormalParamList COMMA FormalParamDecl 
				|
					(SingleFormalParamDecl) FormalParamDecl
				;

FormalParamDecl ::= (ASTFormalParamDecl) Type IDENT:var
				|
					(ArrParamDecl) Type IDENT:var LBRACKET RBRACKET
				;


Type ::= (Type) IDENT:typeName;



StatementList ::= (StmtList) StatementList Statement
				|
				(NoStmt) /* epsilon */
				;

Statement ::= (DesignatorStmt) DesignatorStatement SEMI
				|
				  (IfStatement) IF CondInParens IfPrepare IfInsideStmt
				|
				  (IfElseStatement) IF CondInParens IfPrepare IfInsideStmt Else ElseInsideStmt
				|
				  (DoWhile) DO DoStart Statement WHILE LPAREN Condition RPAREN WhileEnd SEMI
				|
				  (BreakStmt) BREAK SEMI
				|
				  (ContinueStmt) CONTINUE SEMI
				|
				  (ReturnExpr) RETURN Expr SEMI
				|
				  (ReturnNoExpr) RETURN SEMI
				|
				  (ReadStmt) READ LPAREN Designator RPAREN SEMI
				| 
				  (PrintStmt) PRINT LPAREN Expr RPAREN SEMI
				|
				  (PrintStmtNum) PRINT LPAREN Expr COMMA NUMBER:width RPAREN SEMI
				|
				  (SurroundStmt) LBRACE StatementList RBRACE
				;
				
		
				
WhileEnd ::= (WhileEnd) /* epsilon */;				

DoStart ::= (DoStart) /* epsilon */;
				
CondInParens ::= (CondInParens) LPAREN Condition RPAREN;			
				
IfPrepare ::= (IfPrepare) /* epsilon */ ;		
				
IfInsideStmt ::= (IfInsideStmt)	Statement;		

Else ::= (Else)	ELSE;	

ElseInsideStmt ::= (ElseInsideStmt) Statement;
			
DesignatorStatement ::= (AssignStmt) Designator:dest Assignop DesExpr:e
				|
					(FuncCallDes) FuncCall
				|
					(Increment) Designator INC
				|
					(Decrement) Designator DEC
				;


DesExpr ::= (ASTDesExpr) Expr 
					|
			  	    (ErrorAssign) error:e 
			  	    {: parser.report_error("Sintaksna greska. Izvrsen oporavak do ; u liniji " + eleft, null); 
					   parser.synErrors.add(new CompilerError(eleft, "Greska u operaciji dodele. oporavak do ';'", CompilerErrorType.SYNTAX_ERROR));
			        :}
			        ;

ActualPars ::= (Actuals) ActualParamList
				|
				(NoActuals) /* epsilon */
				;
			
ActualParamList ::= (ActualParList) ActualParamList COMMA Expr
				|
					(ActualParam) Expr
				;
				
				
				
Condition ::= (MulCondTerms) Condition OR CondTerm
			| 
			(SingleCondTerm) CondTerm
			;
	
CondTerm ::= (MulCondFacts) CondTerm AND CondFact
			|
			(SingleCondFact) CondFact
			;
		
CondFact ::= (CondFactRel) Expr Relop Expr
			|
			 (ASTCondFact) Expr:expr
			;
				

Expr ::= (OpExpr) TermSum
		|
		 (NegOpExpr) Minus TermSum 
		;
		

Minus ::= (Minus) MINUS;		
		

TermSum ::= (SumTerm) TermSum Addop Term
		|
			(SingleTerm) Term
		;


Term ::=  (MulFactors) Term Muloper Factor
		|
		  (SingleFactor) Factor
		;
		
		
Factor ::= (Var) Designator:d 
		|
			(FuncCallFactor) FuncCall:funcCall
		|
		 	(NumConst) NUMBER 
		| 
			(CharConst) CHAR 
		|
			(BoolConst) BOOL 
		|
			(NewArray) NEW Type LBRACKET ArrSize Expr RBRACKET
		|
			(ExprInParens) LPAREN Expr RPAREN
		;
		
		
ArrSize ::= (ArrSize) /* epsilon */;

FuncCall ::= (FuncCall) Designator:func LPAREN ActualPars RPAREN;

 
Designator ::= (ASTDesignator) IDENT:name
			|	
			   (DesignatorArray) DesArrayName LBRACKET Expr RBRACKET
			;


DesArrayName ::= (DesArrayName) IDENT:name;

Assignop ::= (Assignop) EQUAL ;


Relop ::= (CompEqual) EQ
		|
		  (NotEqual) NEQ
		| 
		  (GreaterThan) GT
		| 
		  (GtEqual) GTEQ
		|
		  (LessThan) LT
		|
		  (LsEqual) LTEQ
		;

Addop ::= (PlusOp) PLUS
		|
		  (MinusOp) MINUS
		;

		
Muloper ::= (Mulop) MUL
		|
		  (Divop) DIV
		|
		  (Modop) MOD
		;



BasicTypes ::= (ASTNumber) NUMBER:numConst
			|
			(ASTBoolean) BOOL:boolConst
			| 
			(ASTCharacter) CHAR:charConst
			;

